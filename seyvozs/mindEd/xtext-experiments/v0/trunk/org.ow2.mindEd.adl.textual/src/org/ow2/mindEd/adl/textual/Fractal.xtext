grammar org.ow2.mindEd.adl.textual.Fractal with org.eclipse.xtext.common.Terminals

generate fractal "http://www.ow2.org/mindEd/adl/textual/Fractal"

import 'http://www.ow2.org/mindEd/itf/' as fractalItf

AdlDefinition :
	imports += ImportDefinition*
	architectureDefinition = ArchitectureDefinition
;

/*
 * SSZ Note: Mind package scoping is handled thanks to the
 * org.ow2.mindEd.adl.textual.scoping.FractalImportedNamespaceAwareLocalScopeProvider
 * to be able to manage implicit declaration during the component declaration.
 */

//// OLD
//AdlDefinition :
//	elements += AbstractElement*
//;
//
//AbstractElement:
//	/* PackageDeclaration | */ ImportDefinition | ArchitectureDefinition
//;

ArchitectureDefinition:
	CompositeDefinition | PrimitiveDefinition | TypeDefinition
;

ImportDefinition:
	(annotationsList=AnnotationsList)?
	'import' importedNamespace = QualifiedNameWithWildcard ';'
;

/*
PackageDeclaration:
	'package' name = QualifiedName ';'
;
*/

QualifiedNameWithWildcard:
	QualifiedName '.*'?
;

CompositeDefinition:
	(annotationsList=AnnotationsList)?
	'composite' name = QualifiedName
	(templateSpecifiersList = TemplateSpecifiersList)? // <ID conformsto TypeDefinition, ID2 conformsto TypeDef2, ...>
	(compositeFormalArgumentsList = FormalArgumentsList)? // (arg1, arg2, ...)
	('extends' superType = [ArchitectureDefinition | QualifiedName])? '{'
		elements += (HostedInterfaceDefinition | SubComponentDefinition | BindingDefinition)*
	'}'
;

PrimitiveDefinition:
	(annotationsList=AnnotationsList)?
	(abstract ?= 'abstract')?
	'primitive' name = QualifiedName
	(compositeFormalArgumentsList = FormalArgumentsList)?
	('extends' superType = [ArchitectureDefinition | QualifiedName])? '{'
		elements += (ProvidedInterfaceDefinition | RequiredInterfaceDefinition | ImplementationDefinition | AttributeDefinition | DataDefinition)*
	'}'
;

TypeDefinition:
	(annotationsList=AnnotationsList)?
	'type' name = QualifiedName ('extends' superType = [ArchitectureDefinition | QualifiedName])? '{'
		elements += (ProvidedInterfaceDefinition | RequiredInterfaceDefinition)*
	'}'
;

HostedInterfaceDefinition:
	ProvidedInterfaceDefinition | RequiredInterfaceDefinition
;

// Renamed as HostedInterfaceDefinition to resolve name clash with fractalItf::InterfaceDefinition
// Another way to solve the problem is to use prefixes ?
ProvidedInterfaceDefinition :
	(annotationsList=AnnotationsList)?
	role="provides" signature = [fractalItf::InterfaceDefinition | QualifiedName] 'as' name=ID
	(collection?='['(collectionsize=INT)?']')? ';'
;

RequiredInterfaceDefinition :
	(annotationsList=AnnotationsList)?
	role="requires" (optional?='optional')? signature = [fractalItf::InterfaceDefinition | QualifiedName] 'as' name=ID
	(collection?='['(collectionsize=INT)?']')? ';'
;

SubComponentDefinition:
	(annotationsList=AnnotationsList)?
	'contains'
	(type = [ArchitectureDefinition | QualifiedName])?
	('<' templatesList+=TemplateDefinition (',' templatesList+=TemplateDefinition)* '>')?
	('(' argumentsList+=ArgumentDefinition (',' argumentsList+=ArgumentDefinition)* ')')?
	'as'
	name = ID
	(body = SubComponentBody)?
	';'
;

SubComponentBody: 
	SubComponentCompositeBody | SubComponentPrimitiveBody
;

SubComponentCompositeBody:
	anonymous ?= 'composite'	// true but the existence of this body in the SubComponentDefinition rule
								// implies an anonymous component anyway
	'{'
		elements += (SubComponentDefinition | ProvidedInterfaceDefinition | RequiredInterfaceDefinition | BindingDefinition)*
	'}'
;

SubComponentPrimitiveBody:
	anonymous?='primitive'		// true but the existence of this body in the SubComponentDefinition rule
								// implies an anonymous component anyway
	'{'
		elements += (ProvidedInterfaceDefinition | RequiredInterfaceDefinition | ImplementationDefinition | AttributeDefinition | DataDefinition)*
	'}'
;

// Check if this rules is in accordance with the original grammar
Element :
	CompositeElement | PrimitiveElement | HostedInterfaceDefinition
;

CompositeElement :
	HostedInterfaceDefinition | SubComponentDefinition | BindingDefinition
;

PrimitiveElement :
	HostedInterfaceDefinition | ImplementationDefinition | AttributeDefinition | DataDefinition
;

// The correct auto-completion scoping for Bindings is implemented in org.ow2.mindEd.adl.textual.scoping.FractalScopeProvider
// This could be assimilated to filtering elements from the references pool to provide only pertinent ones.
BindingDefinition :
	(annotationsList=AnnotationsList)?
	'binds'
	('this' | sourceParent = [SubComponentDefinition | ID])
	'.'
	sourceInterface = [RequiredInterfaceDefinition | ID] ('[' sourceIndex = INT ']')?
	'to'
	('this' | targetParent = [SubComponentDefinition | ID])
	'.'
	targetInterface = [ProvidedInterfaceDefinition | ID] ('[' targetIndex = INT ']')?
	';'
;

FormalArgument :
	name=ID;

FormalArgumentsList :
	'(' formalArguments+=FormalArgument (',' formalArguments+=FormalArgument)* ')';

TemplateSpecifier :
	name=ID 'conformsto' reference = [TypeDefinition | QualifiedName]
;

TemplateSpecifiersList :
	'<' (templateSpecifiers+=TemplateSpecifier (',' templateSpecifiers+=TemplateSpecifier)*) '>';

AttributeType :
	'STRUCT'|'UNION'|'ENUM'|ID;

signedINT :
	('+'|'-')?INT;

HexadecimalType :
	'0x'INT;

Value :
	ID | signedINT | HexadecimalType | STRING | 'null';

TemplateDefinition :
	(name=ID '=')? reference = [CompositeDefinition | QualifiedName]
;

ArgumentDefinition :
	(argumentName = ID '=')? argumentValue = Value
;

AttributeDefinition :
	(annotationsList=AnnotationsList)?
	'attribute' (type = AttributeType)? attributeName = ID ('=' value = Value)?;

ImplementationDefinition :
	(annotationsList=AnnotationsList)?
	'source' ((fileC = FileC) |(inlineCcode = InlineCodeC)) ';'
;

DataDefinition :
	(annotationsList=AnnotationsList)?
	'data' ((fileC = FileC) | (inlineCcode = InlineCodeC)) ';';

QualifiedName:
	ID ('.' ID)*
;

FileC :
	(directory = Path)? name = FileName;
	
InlineCodeC :
	codeC = CODE_C;

Path :
	(ID | ('.') | ('..'))? (SL ((ID ('-')?)* | '..'))* SL;

FileName :
	ID('.'ID)?;

AnnotationsList :
	annotations+=Annotation (annotations+=Annotation)*;

Annotation :
	'@' name=AnnotationType
	('(' annotationElements+=AnnotationElement (',' annotationElements+=AnnotationElement)* ')')?;

// Pre-defined annotations + allow other names
AnnotationType:
	'Override' | 'Singleton' | 'LDFlags' | 'CFlags' | QualifiedName;

AnnotationElement :
	(elementName=ID '=')? elementValue=ElementValue
;

ElementValue :
	ConstantValue | Annotation | ElementValueArrayInitializer;

ElementValueArrayInitializer :
	'{' values+=ElementValue (',' values+=ElementValue)* '}'; 

ConstantValue :
	value = ConstantFormat;

ConstantFormat:
INT|STRING;

terminal CODE_C :
	'{{' -> '}}';

terminal SL :
	(('\\')|('\\\\')|('/'));

//enum Role :
//	provides="provides" | requires="requires";

